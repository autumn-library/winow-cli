#Использовать fs
#Использовать autumn-logos

&Лог("winow.constructor")
Перем Лог;

Перем КаталогШаблонов;

&Желудь
Процедура ПриСозданииОбъекта()
	КаталогШаблонов = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "templates");
КонецПроцедуры

Процедура ПроинициализироватьКаталог(КаталогИнициализации = ".") Экспорт

	ПолныйПуть = ОбъединитьПути(ТекущийКаталог(), КаталогИнициализации);

	СоздатьФайлПропертей(ПолныйПуть);
	СоздатьКаталогПриложений(ПолныйПуть);
	СоздатьКаталогФайлов(ПолныйПуть);
	СоздатьКаталогОтображений(ПолныйПуть);
	СоздатьКаталогВСКод(ПолныйПуть);
	СоздатьОтладочныйСкрипт(ПолныйПуть);

КонецПроцедуры

Процедура СоздатьОтладочныйСкрипт(ПолныйПуть)

	Файл = ОбъединитьПути(ПолныйПуть, "ОтладкаПриложения.os");
	ПеренестиФайлИзШаблона("ОтладкаПриложения.os", Файл);

КонецПроцедуры

Процедура СоздатьКонфигурациюОтладки(ПолныйПуть)

	ФайлОтладки = ОбъединитьПути(ПолныйПуть, "launch.json");
	
	Если ФС.ФайлСуществует(ФайлОтладки) Тогда
		Возврат;
	КонецЕсли;

	ШаблонОтладки = ПрочитатьШаблон("launch.json");

	ЗапускательОтладки = ?(ЭтоВиндовс(), "null", СтрШаблон("""%1""", БинарникОскрипта()));

	ТекстОтладки = СтрШаблон(ШаблонОтладки, ЗапускательОтладки);

	ЗаписьТекста = Новый ЗаписьТекста(ФайлОтладки, КодировкаТекста.UTF8);
	ЗаписьТекста.Записать(ТекстОтладки);
	ЗаписьТекста.Закрыть();

	Лог.Информация(СтрШаблон("Создан файл %1", ФайлОтладки));

КонецПроцедуры

Функция БинарникОскрипта()

	Ошибки = Новый Массив;
	Вывод = Новый Массив;

	Процесс = СоздатьПроцесс("which oscript",,Истина);
	Процесс.Запустить();
	Пока НЕ Процесс.Завершен 
		ИЛИ Процесс.ПотокВывода.ЕстьДанные 
		ИЛИ Процесс.ПотокОшибок.ЕстьДанные Цикл
		
		Приостановить(500);
		
		ОчереднаяСтрокаВывода = Процесс.ПотокВывода.Прочитать();
		ОчереднаяСтрокаОшибок = Процесс.ПотокОшибок.Прочитать();
		Если Не ПустаяСтрока(ОчереднаяСтрокаВывода) Тогда
			Вывод.Добавить(ОчереднаяСтрокаВывода);
		КонецЕсли;
		
		Если Не ПустаяСтрока(ОчереднаяСтрокаОшибок) Тогда
			Ошибки.Добавить(ОчереднаяСтрокаОшибок);
		КонецЕсли;

	КонецЦикла;

	Если Ошибки.Количество() > 0 ИЛИ Вывод.Количество() = 0 Тогда
		Лог.Ошибка(СтрШаблон("Не удалось определить бинарник запуска оскрипта %1", СтрСоединить(Ошибки, Символы.ПС)));
		Возврат "null";

	Иначе 

		Возврат Вывод[0];

	КонецЕсли;
КонецФункции

Функция ЭтоВиндовс()
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат Найти(СистемнаяИнформация.ТипПлатформы, "Windows") > 0;
КонецФункции

Функция ПрочитатьШаблон(ИмяШаблона)
	ФайлШаблона = ОбъединитьПути(КаталогШаблонов, ИмяШаблона);
	ЧтениеТекста = Новый ЧтениеТекста(ФайлШаблона, КодировкаТекста.UTF8);
	Текст = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	Возврат Текст;
КонецФункции

Процедура СоздатьКаталогВСКод(ПолныйПуть)
	Каталог = ОбъединитьПути(ПолныйПуть, ".vscode");
	ОбеспечитьКаталог(Каталог);
	СоздатьКонфигурациюОтладки(Каталог);
КонецПроцедуры

Процедура СоздатьКаталогПриложений(ПолныйПуть)
	Каталог = ОбъединитьПути(ПолныйПуть, "controls");
	ОбеспечитьКаталог(Каталог);
	ПеренестиФайлИзШаблона("ОсновнойКонтрол.os", ОбъединитьПути(Каталог, "ОсновнойКонтрол.os"));
КонецПроцедуры

Процедура СоздатьКаталогФайлов(ПолныйПуть)
	Каталог = ОбъединитьПути(ПолныйПуть, "files");
	ОбеспечитьКаталог(Каталог);
	ПеренестиФайлИзШаблона("wine_and_acorns.jpg", ОбъединитьПути(Каталог, "wine_and_acorns.jpg"));
КонецПроцедуры

Процедура СоздатьКаталогОтображений(ПолныйПуть)
	Каталог = ОбъединитьПути(ПолныйПуть, "view");
	ОбеспечитьКаталог(Каталог);
	ПеренестиФайлИзШаблона("main.html", ОбъединитьПути(Каталог, "main.html"));
	ПеренестиФайлИзШаблона("about.html", ОбъединитьПути(Каталог, "about.html"));
	ПеренестиФайлИзШаблона("contact.html", ОбъединитьПути(Каталог, "contact.html"));
	ПеренестиФайлИзШаблона("index.html", ОбъединитьПути(Каталог, "index.html"));
КонецПроцедуры

Процедура ОбеспечитьКаталог(Каталог)
	Если НЕ ФС.КаталогСуществует(Каталог) Тогда
		ФС.ОбеспечитьКаталог(Каталог);
		Лог.Информация(СтрШаблон("Создан каталог %1", Каталог));
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьФайлПропертей(Путь)

	ФайлПропертей = ОбъединитьПути(Путь, "autumn-properties.json");
	ПеренестиФайлИзШаблона("autumn-properties.json", ФайлПропертей);

КонецПроцедуры

Процедура ПеренестиФайлИзШаблона(ИмяШаблона, КудаПереносить)
	ФайлШаблона = ОбъединитьПути(КаталогШаблонов, ИмяШаблона);

	Если НЕ ФС.ФайлСуществует(КудаПереносить) Тогда
		КопироватьФайл(ФайлШаблона, КудаПереносить);
		Лог.Информация(СтрШаблон("Создан файл %1", КудаПереносить));
	КонецЕсли;
КонецПроцедуры



